
%{
#include "codon.h"
#include "probability.h"
#include "codonmatrix.h"
#include <stdio.h>

typedef char pairbase_type;
typedef int  pairbase_codon_type;

#define BASE_GAP  5
#define BASE_OPEN 6

#define IS_NOT_BASE(a) (a == BASE_GAP ? 1 : a == BASE_OPEN ? 1 : 0)
#define MAKE_PAIRBASE(anchor,informant) (anchor*7+informant)

#define PAIRBASE_LENGTH (7*7)

#define PAIRBASE_CODON_LENGTH (PAIRBASE_LENGTH*PAIRBASE_LENGTH*PAIRBASE_LENGTH)

%}

struct PairBaseModel
Probability base[PAIRBASE_LENGTH]

struct PairBaseCodonModel
Probability codon[PAIRBASE_CODON_LENGTH]

struct PairBaseModelScore
Score base[PAIRBASE_LENGTH]

struct PairBaseCodonModelScore
Score codon[PAIRBASE_CODON_LENGTH]



%{
#include "pairbase.h"

%func
Tweaks a PairBaseCodonModel with on and off diagonal ratios
%%
void diagonal_tweak_PairBaseCodonModel(PairBaseCodonModel * m,double ratio_on,double ratio_off_positive,double ratio_off_negative)
{
  int a,b,c,x,y,z;
  int p;
  int codon_a;
  int codon_b;
  pairbase_type seq[3];

  for(a=0;a<5;a++) {
    for(b=0;b<5;b++) {
      for(c=0;c<5;c++) {
	for(x=0;x<5;x++) {
	  for(y=0;y<5;y++) {
	    for(z=0;z<5;z++) {

	      /* build the sequence */
	      seq[0] = MAKE_PAIRBASE(a,x);
	      seq[1] = MAKE_PAIRBASE(b,y);
	      seq[2] = MAKE_PAIRBASE(c,z);
 
	      p = pairbase_codon_from_seq(seq);



	      if( a == x && b == y && c == z ) {
		if( a == 4 && b == 4 && c== 4 ) {
		  m->codon[p] *= 0.5;
		} else {
		  m->codon[p] *= ratio_on;
		}
	      } else if ( m->codon[p] > 1.0 ) {
		/*	fprintf(stderr,"For %d %d,%d,%d vs %d,%d,%d\n",p,a,b,c,x,y,z); */
		m->codon[p] *= ratio_off_positive;
	      } else {
		m->codon[p] *= ratio_off_negative;
	      }
	    }
	  }
	}
      }
    }
  }
}


%func
flattens out the diagonal signal
%%
void flatten_diagonal_PairBaseCodonModel(PairBaseCodonModel * m,CodonTable * ct)
{
  int a,b,c;
  pairbase_type seq[3];
  int codon_a;
  int p;


  for(a=0;a<5;a++) {
    for(b=0;b<5;b++) {
      for(c=0;c<5;c++) {

	if( a < 4 && b < 4 && c < 4 ) {
	  codon_a = (25 * a) + (5 * b) + c;
	  if( is_stop_codon(codon_a,ct) ) {
	    m->codon[p] = 0.0;
	    continue;
	  }
	}

	/* build the sequence */
	seq[0] = MAKE_PAIRBASE(a,a);
	seq[1] = MAKE_PAIRBASE(b,b);
	seq[2] = MAKE_PAIRBASE(c,c);
	
	
	p = pairbase_codon_from_seq(seq);

	if( a == 4 && b == 4 && c== 4 ) {
	  m->codon[p] = 0.5;
	} else {
	  m->codon[p] = 1.0;
	}
      }
    }
  }

}

%func
flattens out the diagonal signal - for scores!
%%
void flatten_diagonal_PairBaseCodonModelScore(PairBaseCodonModelScore * m,CodonTable * ct)
{
  int a,b,c;
  pairbase_type seq[3];
  int codon_a;
  int p;


  for(a=0;a<5;a++) {
    for(b=0;b<5;b++) {
      for(c=0;c<5;c++) {

	if( a < 4 && b < 4 && c < 4 ) {
	  codon_a = (25 * a) + (5 * b) + c;
	  if( is_stop_codon(codon_a,ct) ) {
	    m->codon[p] = NEGI;
	    continue;
	  }
	}

	/* build the sequence */
	seq[0] = MAKE_PAIRBASE(a,a);
	seq[1] = MAKE_PAIRBASE(b,b);
	seq[2] = MAKE_PAIRBASE(c,c);
	
	
	p = pairbase_codon_from_seq(seq);

	if( a == 4 && b == 4 && c== 4 ) {
	  m->codon[p] = -2;
	} else {
	  m->codon[p] = 0;
	}
      }
    }
  }

}




%func
a 0 pairbasemodel score 
%%
PairBaseModelScore * zero_PairBaseModelScore(void)
{
  PairBaseModelScore * out;
  int i;

  out = PairBaseModelScore_alloc();
  
  for(i=0;i<PAIRBASE_LENGTH;i++) {
    out->base[i] = 0;
  }

  return out;
}
      


%func
Makes a PairBaseCodonModel from just a one parameter! Wow!
%%
PairBaseCodonModel * very_simple_PairBaseCodonModel(Probability id,Probability rnd,Probability nonm,Probability gap,CodonTable * ct)
{
  CompProb * p;
  PairBaseCodonModel * out;

  p = simple_aa_CompProb(id,id,rnd);

  out = make_flat_PairBaseCodonModel(p,nonm,gap,ct);
  
  free_CompProb(p);

  return out;
}


%func
Makes a PairBaseCodonModel from a protein matrix - assumming a flat 
mapping to CodonMatrix
%%
PairBaseCodonModel * make_flat_PairBaseCodonModel(CompProb * cp,Probability nonm,Probability gap,CodonTable * ct)
{
  CodonMatrix * cm;
  PairBaseCodonModel * out;

  cm = naive_CodonMatrix(ct,cp);

  out = make_PairBaseCodonModel(cm,nonm,gap,ct);

  free_CodonMatrix(cm);

  return out;
}

%func
Makes a PairBaseCodonModel score for start codon
%%
PairBaseCodonModelScore * make_start_PairBaseCodonModelScore(CodonTable * ct)
{
  PairBaseCodonModel * model;
  PairBaseCodonModelScore * out;

  model = make_conserved_PairBaseCodonModel(100.0,0.000000000001,'M',ct);
  
  out = new_PairBaseCodonModelScore(model);

  free_PairBaseCodonModel(model);
  
  return out;

}

%func
Makes a PairBaseCodonModel score for start codon
%%
PairBaseCodonModelScore * make_stop_PairBaseCodonModelScore(CodonTable * ct)
{
  PairBaseCodonModel * model;
  PairBaseCodonModelScore * out;

  model = make_conserved_PairBaseCodonModel(100.0,0.000000000001,'X',ct);
  
  out = new_PairBaseCodonModelScore(model);

  free_PairBaseCodonModel(model);
  
  return out;

}


%func
Makes a PairBaseCodonModel for a particular character in the CodonTable
%%
PairBaseCodonModel * make_conserved_PairBaseCodonModel(Probability cons,Probability non_cons,char aa_var,CodonTable * ct)
{
  PairBaseCodonModel * out;
  int i;
  base base_a,base_b,base_c;
  pairbase_type seq[3];
  int p;

  assert(ct);

  out = PairBaseCodonModel_alloc();

  for(i=0;i<PAIRBASE_CODON_LENGTH;i++) {
    out->codon[i] = non_cons;
  }

  for(i=0;i<125;i++) {
    if( ct->codon_str[i] == aa_var ) {
      fprintf(stderr,"Assinging %d with %c\n",i,aa_var);
      all_bases_from_codon(i,&base_a,&base_b,&base_c);
      seq[0] = MAKE_PAIRBASE(base_a,base_a);
      seq[1] = MAKE_PAIRBASE(base_b,base_b);
      seq[2] = MAKE_PAIRBASE(base_c,base_c);
      
      p = pairbase_codon_from_seq(seq);
      out->codon[p] = cons;
    }
  }

  return out;
}


%func
Makes a PairBaseCodonModel from a CodonMatrix and parameters
%%
PairBaseCodonModel * make_PairBaseCodonModel(CodonMatrix * codon_matrix,Probability nonm,Probability gap,CodonTable * ct)
{
  PairBaseCodonModel * out;
  int a,b,c,x,y,z;
  int i;
  int codon_a;
  int codon_b;
  int p;

  pairbase_type seq[3];


  assert(codon_matrix);
  assert(ct);

  out = PairBaseCodonModel_alloc();

  for(i=0;i<PAIRBASE_CODON_LENGTH;i++) {
    out->codon[i] = 0.0;
  }

  for(a=0;a<5;a++) {
    for(b=0;b<5;b++) {
      for(c=0;c<5;c++) {
	for(x=0;x<5;x++) {
	  for(y=0;y<5;y++) {
	    for(z=0;z<5;z++) {

	      /* build the sequence */
	      seq[0] = MAKE_PAIRBASE(a,x);
	      seq[1] = MAKE_PAIRBASE(b,y);
	      seq[2] = MAKE_PAIRBASE(c,z);
 
	      p = pairbase_codon_from_seq(seq);

	      codon_a = (a * 25) + (b * 5) + c;
	      codon_b = (x * 25) + (y * 5) + z;
 
	      if( is_stop_codon(codon_a,ct) || is_stop_codon(codon_b,ct) ) {
		out->codon[p] = 0.0;
		continue;
	      }

	      /* else */
	      out->codon[p] = codon_matrix->prob[codon_a][codon_b];
	      
	    }
	  }
	}
      }
    }
  }

  /* now to do blank and gap scores */

  for(a=0;a<5;a++) {
    for(b=0;b<5;b++) {
      for(c=0;c<5;c++) {
  
	codon_a = (a * 25) + (b * 5) + c;
	      
	seq[0] = MAKE_PAIRBASE(a,BASE_GAP);
	seq[1] = MAKE_PAIRBASE(b,BASE_GAP);
	seq[2] = MAKE_PAIRBASE(c,BASE_GAP);
 
	p = pairbase_codon_from_seq(seq);
       
	if( is_stop_codon(codon_a,ct) ) {
	  out->codon[p] = 0.0;
	} else { 
	  out->codon[p] = gap;
	}
	      
	seq[0] = MAKE_PAIRBASE(BASE_GAP,a);
	seq[1] = MAKE_PAIRBASE(BASE_GAP,b);
	seq[2] = MAKE_PAIRBASE(BASE_GAP,c);
 
	p = pairbase_codon_from_seq(seq);
       
	if( is_stop_codon(codon_a,ct) ) {
	  out->codon[p] = 0.0;
	} else { 
	  out->codon[p] = gap;
	}

	      
	seq[0] = MAKE_PAIRBASE(a,BASE_OPEN);
	seq[1] = MAKE_PAIRBASE(b,BASE_OPEN);
	seq[2] = MAKE_PAIRBASE(c,BASE_OPEN);
 
	p = pairbase_codon_from_seq(seq);
       
	if( is_stop_codon(codon_a,ct) ) {
	  out->codon[p] = 0.0;
	} else { 
	  out->codon[p] = nonm;
	}
	      
	seq[0] = MAKE_PAIRBASE(BASE_OPEN,a);
	seq[1] = MAKE_PAIRBASE(BASE_OPEN,b);
	seq[2] = MAKE_PAIRBASE(BASE_OPEN,c);
 
	p = pairbase_codon_from_seq(seq);
       
	if( is_stop_codon(codon_a,ct) ) {
	  out->codon[p] = 0.0;
	} else { 
	  out->codon[p] = nonm;
	}
      }
    }
  }

  return out;
}



%func
Makes a pair base model from simple leading diagonal
%%
PairBaseModel * simple_PairBaseModel(Probability iden,Probability other,Probability gap)
{
  PairBaseModel * out;
  int i;
  int j;
  int base;


  out = PairBaseModel_alloc();

  for(i=0;i<7;i++) {
    for(j=0;j<7;j++) {
      base = (i*7)+j;
      if( i == 5 || j == 5 ) {
	out->base[base]= gap/0.25;
      } else if( i == 6 || j == 6 ) {
	out->base[base]= 0.0;
      } else if( i == 4 || j == 4 ) {
	out->base[base]= 1.0;
      } else if( i == j ) {
	out->base[base]= iden / 0.25;
      } else {
	out->base[base]= other / 0.25;
      }
    }
  }

  return out;
}



%func
Makes a codon score from a codon model
%%
PairBaseCodonModelScore * new_PairBaseCodonModelScore(PairBaseCodonModel * pbcm)
{
  PairBaseCodonModelScore * out;

  out = PairBaseCodonModelScore_alloc();

  Probability2Score_move(pbcm->codon,out->codon,PAIRBASE_CODON_LENGTH);

  return out;
}

%func
Makes a base score from a base model
%%
PairBaseModelScore * new_PairBaseModelScore(PairBaseModel * pbm)
{
  PairBaseModelScore * out;

  out = PairBaseModelScore_alloc();

  Probability2Score_move(pbm->base,out->base,PAIRBASE_LENGTH);

  return out;
}

%func
Debugging
%%
void show_PairBaseModelScore(PairBaseModelScore * sc,FILE * ofp)
{
  int i;
  int anchor;
  int informant;
    

  for(i=0;i<PAIRBASE_LENGTH;i++) {
    anchor =anchor_base_from_pairbase(i);
    informant = informant_base_from_pairbase(i);

    fprintf(ofp," %2d %c %c %d\n",i,char_for_base(anchor),char_for_base(informant),sc->base[i]);
  }

}

%func 
Debugging
%%
void show_PairBaseCodonModelScore(PairBaseCodonModelScore * sc,CodonTable * ct,FILE * ofp)
{
  int i;
  pairbase_type a;
  pairbase_type b;
  pairbase_type c;

  int anchor_a;
  int anchor_b;
  int anchor_c;

  int informant_a;
  int informant_b;
  int informant_c;

  char seq1[4];
  char seq2[4];

  seq1[3] = seq2[3] = '\0';

  for(i=0;i<PAIRBASE_CODON_LENGTH;i++) {
    decompose_pairbase_codon(i,&a,&b,&c);

    anchor_a = anchor_base_from_pairbase(a);
    anchor_b = anchor_base_from_pairbase(b);
    anchor_c = anchor_base_from_pairbase(c);

    informant_a = informant_base_from_pairbase(a);
    informant_b = informant_base_from_pairbase(b);
    informant_c = informant_base_from_pairbase(c);

    seq1[0] = char_for_base(anchor_a);
    seq1[1] = char_for_base(anchor_b);
    seq1[2] = char_for_base(anchor_c);

    seq2[0] = char_for_base(informant_a);
    seq2[1] = char_for_base(informant_b);
    seq2[2] = char_for_base(informant_c);


    fprintf(ofp,"%9d %s[%c] %s[%c] : %d\n",i,seq1,aminoacid_from_seq(ct,seq1),seq2,aminoacid_from_seq(ct,seq2),sc->codon[i]);

  }
  
}

%func
Inverts pairbase codon
%%
pairbase_codon_type reverse_pairbase_codon(pairbase_codon_type codon)
{
  pairbase_type a;
  pairbase_type b;
  pairbase_type c;
  
  decompose_pairbase_codon(codon,&a,&b,&c);

  a = complement_pairbase(a);
  b = complement_pairbase(b);
  c = complement_pairbase(c);

  return (c*(PAIRBASE_LENGTH*PAIRBASE_LENGTH))+(b*PAIRBASE_LENGTH)+a;
}


%func
complements a pairbase 
%%
pairbase_type complement_pairbase(pairbase_type b)
{
  pairbase_type anchor;
  pairbase_type informant;

  anchor = anchor_base_from_pairbase(b);
  informant = informant_base_from_pairbase(b);

  /* we reverse completement anchor */
  
  anchor = complement_base(anchor);

  if( informant != BASE_GAP && informant != BASE_OPEN ) {
    informant = complement_base(informant);
  }

  return MAKE_PAIRBASE(anchor,informant);
}


%func
Makes a pairbase_codon from a pairbase_sequence
%%
pairbase_codon_type pairbase_codon_from_seq(pairbase_type * seq)
{
  pairbase_type one;
  pairbase_type two;
  pairbase_type three;

  one   = (*seq);
  two   = (*(seq+1));
  three = (*(seq+2));

  return (one*(PAIRBASE_LENGTH*PAIRBASE_LENGTH))+(two*PAIRBASE_LENGTH)+three;
}



%func
Decomposes a pairbase codon
%%
void decompose_pairbase_codon(pairbase_codon_type t,pairbase_type * a,pairbase_type * b,pairbase_type * c)
{
  assert(a);
  assert(b);
  assert(c);
    
  *a = t/(PAIRBASE_LENGTH*PAIRBASE_LENGTH);
  t -= (*a) * (PAIRBASE_LENGTH*PAIRBASE_LENGTH);

  *b = t/PAIRBASE_LENGTH;
  t -= (*b) * PAIRBASE_LENGTH;

  *c = t;

}


%func
Finds the anchor base from a pair base
%%
base anchor_base_from_pairbase(pairbase_type pairbase)
{
  int top;

  top = (int)pairbase / 7;

  return top;
}

%func
Finds the informant base from a pair base
%%
base informant_base_from_pairbase(pairbase_type pairbase)
{
  int top;

  top = (int) pairbase /7;
  pairbase -= top*7;

  return pairbase;
}


%func
gives back the character for the base
%%
char char_for_base(int base)
{
  if( base < 5 ) {
    return char_from_base(base);
  } 
  if( base == BASE_GAP ) {
    return '-';
  } else {
    return ' ';
  }
}
  









